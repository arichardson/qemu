#
# SPDX-License-Identifier: BSD-2-Clause
#
# Copyright (c) 2020 Alex Richardson
# All rights reserved.
#
# This software was developed by SRI International and the University of
# Cambridge Computer Laboratory (Department of Computer Science and
# Technology) under DARPA contract HR0011-18-C-0016 ("ECATS"), as part of the
# DARPA SSITH research programme.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in the
#    documentation and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR AND CONTRIBUTORS ``AS IS'' AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
# OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
# HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
# LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
# OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
#

# This is concatenated with insn32.decode for risc64 targets.
# Most of the fields and formats are there.

%cs2       20:5
%cs1       15:5
%cd         7:5

&ccr cd cs1 rs2
@ccr .......   ..... ..... ... ..... ....... &ccr %cd %cs1 %rs2
&rcc rd cs1 cs2
@rcc .......   ..... ..... ... ..... ....... &rcc %rd %cs1 %cs2
&ccc cd cs1 cs2
@ccc .......   ..... ..... ... ..... ....... &ccc %cd %cs1 %cs2

&rc rd cs1
@rc .......  ..... ..... ... ..... ....... &rc %rd %cs1
@cc .......  ..... ..... ... ..... ....... &cc %cd %cs1


### Three operand instructions
{
  cmv   0000110  00000 ..... 000 ..... 0110011 @cc
  cadd  0000110  ..... ..... 000 ..... 0110011 @ccr
}
scaddr  0000110  ..... ..... 001 ..... 0110011 @ccr
acperm  0000110  ..... ..... 010 ..... 0110011 @ccr
schi    0000110  ..... ..... 011 ..... 0110011 @ccr
sceq    0000110  ..... ..... 100 ..... 0110011 @rcc
cbld    0000110  ..... ..... 101 ..... 0110011 @ccc
scss    0000110  ..... ..... 110 ..... 0110011 @rcc
scbnds  0000111  ..... ..... 000 ..... 0110011 @ccr
scbndsr 0000111  ..... ..... 001 ..... 0110011 @ccr
scmode  0000110  ..... ..... 111 ..... 0110011 @ccr ?pred_hybrid

### Two operand instructions
gctag       0001000  00000 ..... 000 ..... 0110011 @rc
gcperm      0001000  00001 ..... 000 ..... 0110011 @rc
gctype      0001000  00010 ..... 000 ..... 0110011 @rc
gcmode      0001000  00011 ..... 000 ..... 0110011 @rc ?pred_hybrid
gchi        0001000  00100 ..... 000 ..... 0110011 @rc
gcbase      0001000  00101 ..... 000 ..... 0110011 @rc
gclen       0001000  00110 ..... 000 ..... 0110011 @rc
cram        0001000  00111 ..... 000 ..... 0110011 @r2
sentry      0001000  01000 ..... 000 ..... 0110011 @cc

### Zero operand instructions
modesw_cap 0001001 00000 00000 001 00000 0110011 ?pred_hybrid
modesw_int 0001010 00000 00000 001 00000 0110011 ?pred_hybrid

### Instructions with immediate operands
@cci ............    ..... ... ..... ....... &cci %cd %cs1 imm=%imm_i
caddi         ............   ..... 010 ..... 0011011 @cci

# The RISC-V CHERI spec adds a new R format with a scaled unsigned immediate for scbndsi
%imm5_unsigned 20:5
%imm_scale     25:1
@is_unsigned  ...... . ..... ..... ... ..... ....... scale=%imm_scale imm=%imm5_unsigned %cs1 %cd
scbndsi       000001 . ..... ..... 101 ..... 0010011 @is_unsigned


# Loads and stores
lc       ............   ..... 100 ..... 0001111 @i
sc       ....... .....  ..... 100 ..... 0100011 @s

# Atomic instructions
amoswap_c 00001 . . ..... ..... 100 ..... 0101111 @atom_st
lr_c      00010 . . 00000 ..... 100 ..... 0101111 @atom_ld
sc_c      00011 . . ..... ..... 100 ..... 0101111 @atom_st